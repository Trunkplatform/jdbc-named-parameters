plugins {
    id "com.jfrog.bintray" version "1.2"
}
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group = 'com.trunk.jdbc'
version = '1.0.2'

description = 'Provides named query parameter capability to JDBC PreparedStatements'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://oss.jfrog.org/libs-snapshot' }
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile 'com.google.guava:guava:16.0.1'
}

test {
    beforeTest { descriptor ->
        logger.lifecycle("Running: " + descriptor)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
                        dep.name == it.artifactId.text()
                    }
                }.each() {
                    it.scope*.value = 'compile'
                }
            }
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}


apply from: './bintray.gradle'
